
-- =============================================
-- Autor:	rudi.garcia
-- Fecha de Creacion: 	27-Nov-2017 @ Team Reborn - Sprint Nach
-- Description:	 Sp el pase de salida para el reporte

/*
-- Ejemplo de Ejecucion:
EXEC [FERCO].[OP_WMS_SP_GET_PASS_FOR_REPORT1] @PASS_ID = 11333,  @DISTRIBUTION_CENTER_ID = 'CEDI_GT'
*/
-- =============================================

CREATE PROCEDURE [FERCO].[OP_WMS_SP_GET_PASS_FOR_REPORT1] (
		@PASS_ID INT
		,@DISTRIBUTION_CENTER_ID VARCHAR(50)
	)
AS
BEGIN
	SET NOCOUNT ON;
  --
  
	DECLARE	@COMPANY_NAME VARCHAR(150)
	, @DOC_NUM_FLETE VARCHAR(100);

	SELECT TOP 1
		@COMPANY_NAME = [SC].[COMPANY_NAME]
	FROM
		[FERCO].[OP_SETUP_COMPANY] [SC];


	DECLARE	@LOGINS TABLE ([LOGIN_ID] VARCHAR(25));

	INSERT	INTO @LOGINS
			(
				[LOGIN_ID]
			)
	SELECT DISTINCT
		[WU].[LOGIN_ID]
	FROM
		[FERCO].[OP_WMS_WAREHOUSES] [W]
	INNER JOIN [FERCO].[OP_WMS_WAREHOUSE_BY_USER] [WU] ON ([W].[WAREHOUSE_ID] = [WU].[WAREHOUSE_ID])
	WHERE
		[W].[DISTRIBUTION_CENTER_ID] = @DISTRIBUTION_CENTER_ID;

	
	SELECT
		[PD].[DOC_NUM]
		,[PD].[PICKING_DEMAND_HEADER_ID]
		,[PD].[PASS_HEADER_ID]
		,[PD].[CLIENT_CODE]
		,[PD].[CLIENT_NAME]
		,[PD].[WAVE_PICKING_ID]
		,ISNULL([CMP].[COMPONENT_MATERIAL],
				[PD].[MATERIAL_ID]) [MATERIAL_ID]
		,ISNULL([M].[MATERIAL_NAME], [PD].[MATERIAL_NAME]) [MATERIAL_NAME]
		,ISNULL(MAX([CMP].[QTY]), 1) * SUM([PD].[QTY]) AS [QTY]
	INTO
		[#DETAIL]
	FROM
		[FERCO].[OP_WMS_PASS_DETAIL] [PD]
	LEFT JOIN [FERCO].[OP_WMS_COMPONENTS_BY_MASTER_PACK] [CMP] ON [CMP].[MASTER_PACK_CODE] = [PD].[MATERIAL_ID]
	LEFT JOIN [FERCO].[OP_WMS_MATERIALS] [M] ON [CMP].[COMPONENT_MATERIAL] = [M].[MATERIAL_ID]
	WHERE
		[PD].[PASS_HEADER_ID] = @PASS_ID
		
		GROUP BY ISNULL([CMP].[COMPONENT_MATERIAL], [PD].[MATERIAL_ID])
				,ISNULL([M].[MATERIAL_NAME], [PD].[MATERIAL_NAME])
				,[PD].[DOC_NUM]
				,[PD].[PICKING_DEMAND_HEADER_ID]
				,[PD].[PASS_HEADER_ID]
				,[PD].[CLIENT_CODE]
				,[PD].[CLIENT_NAME]
				,[PD].[WAVE_PICKING_ID]
		;


	SELECT  TOP 1 @DOC_NUM_FLETE = P.[NUMERO_ORDEN]  FROM [FERCO].[OP_WMS_TASK_LIST] t 
	INNER JOIN [#DETAIL] ON [#DETAIL].[WAVE_PICKING_ID] = [t].[WAVE_PICKING_ID]
	INNER JOIN [FERCO].[OP_WMS_POLIZA_HEADER] p ON t.[CODIGO_POLIZA_SOURCE] = p.[CODIGO_POLIZA]

	 
	
	SELECT DISTINCT
    --Encabezado
		[P].[PASS_ID]
		,@COMPANY_NAME AS [COMPANY_NAME]
		,@DISTRIBUTION_CENTER_ID AS [DISTRIBUTION_CENTER_ID]
		,[P].[HANDLER]
		,[P].[AUTORIZED_BY]
		,[P].[CREATED_DATE] [LAST_UPDATED]
		,(CASE	WHEN [P].[TYPE] = 'SALES_ORDER' THEN 'VENTA'
				WHEN [P].[TYPE] = 'TRANSFER_REQUEST'
				THEN 'TRANSFERENCIA'
				WHEN [P].[TYPE] = 'GENERAL_DISPATCH'
				THEN 'DESPACHO GENERAL'
			END) [TYPE]
    --Detalle 
		,[PD].[CLIENT_CODE]
		,[PD].[CLIENT_NAME]
		,[PD].[WAVE_PICKING_ID]
		,(CASE	WHEN [P].[TYPE] = 'TRANSFER_REQUEST'
				THEN [PDH].[DOC_NUM_SEQUENCE] 
				WHEN [P].[TYPE] = 'GENERAL_DISPATCH'
				THEN  [PD].[DOC_NUM] 
				ELSE  ISNULL([TRH].[DOC_NUM], [PD].[DOC_NUM]) 
			END) [DOC_NUM]
		,ISNULL([PDH].[ERP_REFERENCE] , @DOC_NUM_FLETE) [ERP_REFERENCE]
		,[PD].[MATERIAL_ID]
		,[PD].[MATERIAL_NAME]
		,[PD].[QTY] [QTY]
		,[P].[VEHICLE_PLATE]
		,[P].[VEHICLE_DRIVER]
		,ISNULL([TRH].[WAREHOUSE_TO],
				[PDH].[ADDRESS_CUSTOMER]) [ADDRESS_CUSTOMER]
	FROM
		[FERCO].[OP_WMS3PL_PASSES] [P]
	INNER JOIN @LOGINS [L] ON (
								[L].[LOGIN_ID] = [P].[CREATED_BY]
								OR [L].[LOGIN_ID] = [P].[LAST_UPDATED_BY]
								)
	LEFT JOIN [#DETAIL] [PD] ON ([P].[PASS_ID] = [PD].[PASS_HEADER_ID])
	LEFT JOIN [FERCO].[OP_WMS_NEXT_PICKING_DEMAND_HEADER] [PDH] ON ([PD].[PICKING_DEMAND_HEADER_ID] = [PDH].[PICKING_DEMAND_HEADER_ID])
	LEFT JOIN [FERCO].[OP_WMS_TRANSFER_REQUEST_HEADER] [TRH] ON [TRH].[TRANSFER_REQUEST_ID] = [PDH].[TRANSFER_REQUEST_ID]
	WHERE
		[P].[PASS_ID] = @PASS_ID


END;








