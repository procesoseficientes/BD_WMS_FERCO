-- =============================================
-- Autor:				pablo.aguilar
-- Fecha de Creacion: 	13-Aug-18 @ Nexus Team Sprint @FocaMonje 
-- Description:			SP que 
/*
-- Ejemplo de Ejecucion:
				EXEC [FERCO].[OP_WMS_BI_DISPATCH_ANALYSIS]
				TRUNCATE TABLE [SCM_BI].[dbo].[DISPATCH_ANALYSIS]
				
*/
-- =============================================
CREATE PROCEDURE [FERCO].[OP_WMS_BI_DISPATCH_ANALYSIS]
AS
BEGIN
	
	--
	DECLARE	@LAST_DATE_LOGGED DATE  = '00010101';
	
	SELECT TOP 1
		@LAST_DATE_LOGGED =  [COMPLETED_DATE]
	FROM
		[SCM_BI].[dbo].[DISPATCH_ANALYSIS]
	ORDER BY
		[COMPLETED_DATE] DESC;	
	
	SELECT
		[WAVE_PICKING_ID]
		,MIN([ACCEPTED_DATE]) [ACCEPTED_DATE]
		,MAX([COMPLETED_DATE]) [COMPLETED_DATE]
		,MIN([ASSIGNED_DATE]) [ASSIGNED_DATE]
		,DATEDIFF(MINUTE, MIN([ACCEPTED_DATE]),
					MAX([COMPLETED_DATE])) [TOTAL_MINUTES]
		,[TASK_OWNER]
		,[L].[LOGIN_NAME] [TASK_OWNER_NAME]
	INTO
		[#TASKS]
	FROM
		[FERCO].[OP_WMS_TASK_LIST]
	INNER JOIN [FERCO].[OP_WMS_LOGINS] [L] ON [L].[LOGIN_ID] = [TASK_OWNER]
	WHERE
		CAST( [COMPLETED_DATE] AS DATE) > @LAST_DATE_LOGGED
		AND [ACCEPTED_DATE] IS NOT NULL
		AND [IS_CANCELED] = 0
		AND [QUANTITY_ASSIGNED] <> [QUANTITY_PENDING]
		AND [TASK_TYPE] = 'TAREA_PICKING'
	GROUP BY
		[WAVE_PICKING_ID]
		,[TASK_OWNER]
		,[L].[LOGIN_NAME]
	HAVING
		MIN([IS_COMPLETED]) = 1;
	
	
	
	INSERT INTO [SCM_BI].[dbo].[DISPATCH_ANALYSIS]
	SELECT
		[T].[WAVE_PICKING_ID]
		,[T].[ACCEPTED_DATE]
		,[T].[COMPLETED_DATE]
		,[T].[ASSIGNED_DATE]
		,[T].[TOTAL_MINUTES]
		,[T].[TASK_OWNER]
		,[T].[TASK_OWNER_NAME]
		,CASE	WHEN [D].[SOURCE_TYPE] = 'WT - ERP'
				THEN [D].[DOC_NUM_SEQUENCE]
				ELSE [D].[DOC_NUM]
			END [DOC_NUM]
		,[D].[IS_CONSOLIDATED]
		,[D].[DEMAND_TYPE]
		,[D].[IS_POSTED_ERP]
		,[D].[CLIENT_CODE]
		,[D].[CLIENT_NAME]
		,[D].[DEMAND_DELIVERY_DATE]
		,[D].[ADDRESS_CUSTOMER]
		,[D].[CODE_WAREHOUSE]
		,[D].[TYPE_DEMAND_NAME]
		,[D].[ERP_REFERENCE_DOC_NUM]
	FROM
		[#TASKS] [T]
	INNER JOIN [FERCO].[OP_WMS_NEXT_PICKING_DEMAND_HEADER] [D] ON [D].[WAVE_PICKING_ID] = [T].[WAVE_PICKING_ID];
	
	



END;


